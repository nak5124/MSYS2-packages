# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Yuta Nakai <nak5124@live.jp>

pkgname=tmux-git
_ver_base=1.9
pkgver=1.9.4926.747cab4
pkgrel=1
pkgdesc='A terminal multiplexer'
url='http://tmux.sourceforge.net/'
arch=('i686' 'x86_64')
license=('BSD')
provides=('tmux')
conflicts=('tmux')
depends=('ncurses' 'libevent')
makedepends=('git' 'ncurses-devel' 'libevent-devel')
source=("$pkgname"::'git+http://git.code.sf.net/p/tmux/tmux-code'
        'LICENSE'
        '0001-msys-platform.patch'
        '0002-fix-declaration-of-MAXPATHLEN.patch'
        '0003-use_acs.patch'
        '0004-utf8_width.patch'
        '0005-copy_cursor_width.patch')
md5sums=('SKIP'
         '71601bc37fa44e4395580b321963018e'
         'b175ece57d04168245f63dbf51f87cd0'
         '7b8ce74544c7e918f712e7114145d98e'
         'b5a79f71f85c51b2973058a0be9a46ab'
         '14fee957c94a892c80d2073b96ce8d82'
         '56d1ab216a5395eb865f6fa7dd490459')

pkgver() {
  cd ${srcdir}/${pkgname}
  printf "%s.%s.%s" "$_ver_base" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${srcdir}/${pkgname}
  patch -p1 -i ${srcdir}/0001-msys-platform.patch
  patch -p1 -i ${srcdir}/0002-fix-declaration-of-MAXPATHLEN.patch
  patch -p1 -i ${srcdir}/0003-use_acs.patch
  patch -p1 -i ${srcdir}/0004-utf8_width.patch
  patch -p1 -i ${srcdir}/0005-copy_cursor_width.patch
  cp -fa osdep-cygwin.c osdep-msys.c
  ./autogen.sh
}

build() {
  cd ${srcdir}/${pkgname}
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    CPPFLAGS="${CPPFLAGS} -I/usr/include/ncursesw"
  make
}

package() {
  cd ${srcdir}/${pkgname}
  make DESTDIR=${pkgdir} install
  install -Dm644 ../LICENSE ${pkgdir}/usr/share/licenses/tmux/LICENSE

  install -dm755 ${pkgdir}/usr/share/tmux/
  install -m644 examples/* ${pkgdir}/usr/share/tmux/
  install -Dm644 examples/tmux.vim ${pkgdir}/usr/share/vim/vimfiles/syntax/tmux.vim

  install -d ${pkgdir}/usr/share/bash-completion/completions/
  mv ${pkgdir}/usr/share/tmux/bash_completion_tmux.sh ${pkgdir}/usr/share/bash-completion/completions/tmux
}
